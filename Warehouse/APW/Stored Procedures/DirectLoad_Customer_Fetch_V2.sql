
-- =============================================
-- Author:		JEA
-- Create date: 03/11/2016
-- Description:	Retrieves customers from staging area for direct load
-- =============================================

CREATE PROCEDURE [APW].[DirectLoad_Customer_Fetch_V2] 

AS
BEGIN

	SET NOCOUNT ON;

	---------------------------------------------------------------------------------------
	---------------Load intial list of customers to be exported to RewardBI----------------
	---------------------------------------------------------------------------------------

		IF OBJECT_ID('tempdb..#AgreedTCs') IS NOT NULL DROP TABLE #AgreedTCs
		SELECT	FanID
			,	[Date] AS AgreedTCs
		INTO #AgreedTCs
		FROM [Staging].[InsightArchiveData] iad
		WHERE iad.TypeID = 1

		CREATE CLUSTERED INDEX CIX_FanID ON #AgreedTCs (FanID)

		TRUNCATE TABLE [APW].[DirectLoad_Staging_Customer]
		INSERT INTO [APW].[DirectLoad_Staging_Customer]
		SELECT	FanID = fa.ID
			,	CustStatus = fa.[Status]
			,	DOB = fa.DOB
			,	ActivatedDate = CONVERT(DATE, COALESCE(ca.ActivatedDate, atc.AgreedTCs, fa.AgreedTCsDate))
			,	Gender = CONVERT(CHAR(1),	CASE
												WHEN fa.Sex = 1 THEN 'M'
												WHEN fa.Sex = 2 THEN 'F'
												ELSE 'U'
											END)
			,	DeactivatedDate =	CASE
										WHEN fa.[Status] = 0 OR fa.AgreedTCs = 0 OR fa.AgreedTCsDate IS NULL THEN COALESCE(ca.OptedOutDate,ca.DeactivatedDate)
										ELSE NULL
									END	
		FROM [SLC_Report].[dbo].[Fan] fa
		LEFT JOIN #AgreedTCs atc
			ON fa.ID = atc.FanID
		LEFT JOIN [MI].[CustomerActiveStatus] ca
			ON fa.ID = ca.FanID
		WHERE fa.ClubID IN (132, 138)
		AND (fa.AgreedTCs = 1 OR atc.AgreedTCs IS NOT NULL)
		AND fa.ID NOT IN (19587579)
		AND NOT EXISTS (	SELECT 1
							FROM [Staging].[Customer_TobeExcluded] ctbe
							WHERE fa.ID = ctbe.FanID)

	---------------------------------------------------------------------------------------
	-----------Find those customers who are deactivated with no DeactivatedDate------------
	---------------------------------------------------------------------------------------

		/*DeactivatedDate is populated off of a table end produces based on an assessment of the
		  changelog, therefore I am finding dates for those catered for by this*/
	  
		IF OBJECT_ID('tempdb..#DeactivatedCustomers') IS NOT NULL DROP TABLE #DeactivatedCustomers
		SELECT	FanID
			,	ActivationDate
		INTO #DeactivatedCustomers 
		FROM [APW].[DirectLoad_Staging_Customer]
		WHERE CustStatus = 0
		AND DeactivationDate IS NULL

		CREATE CLUSTERED INDEX CIX_FanID ON #DeactivatedCustomers (FanID)


	---------------------------------------------------------------------------------------
	-----------------Find comment that indicates the Fan was Deactivated-------------------
	---------------------------------------------------------------------------------------

		/*This comment is normally generated by an overnight process checking valid Pans and 
		  Fans etc*/

		IF OBJECT_ID('tempdb..#Comm_Deact') IS NOT NULL DROP TABLE #Comm_Deact
		SELECT	co.FanID
			,	Deact_Date = MAX(co.[Date])
		INTO #Comm_Deact
		FROM [SLC_Report].[dbo].[Comments] co
		WHERE co.Comment LIKE 'Fan Deactivated%'
		AND EXISTS (SELECT 1
					FROM #DeactivatedCustomers dc
					WHERE co.FanID = dc.FanID
					AND dc.ActivationDate <= co.[Date])
		GROUP BY	co.FanID

		CREATE CLUSTERED INDEX CIX_FanID ON #Comm_Deact (FanID)
		

		IF OBJECT_ID('tempdb..#DeactTable') IS NOT NULL DROP TABLE #DeactTable
		SELECT	dc.FanID
			,	Deact_Date = MIN(c.DataDate)
		INTO #DeactTable
		FROM #DeactivatedCustomers dc
		INNER JOIN [Warehouse].[Staging].[DeactivatedCustomers] c
			ON dc.FanID = c.FanID
		WHERE NOT EXISTS (	SELECT 1
							FROM #Comm_Deact cd
							WHERE dc.FanID = cd.FanID)
		GROUP BY dc.FanID
		HAVING '2012-07-17' < MIN(c.DataDate) -- ignore those from first week.

		CREATE CLUSTERED INDEX CIX_FanID ON #DeactTable (FanID)

	---------------------------------------------------------------------------------------
	----------------------Find other Comment entries to use for date-----------------------
	---------------------------------------------------------------------------------------
	
		IF OBJECT_ID('tempdb..#Comm_Deact2') IS NOT NULL DROP TABLE #Comm_Deact2
		SELECT	FanID = cm.ObjectID
			,	Deact_Date = MAX(cm.[Date])
		INTO #Comm_Deact2
		FROM [SLC_Report].[dbo].[Comments] cm
		WHERE (		cm.Comment LIKE '%Opt_Out%'
				OR	cm.Comment LIKE '%Account_Close%'
				OR	cm.Comment LIKE '%Close_Account%'
				OR	cm.Comment LIKE '%Disabled%'
				OR	cm.Comment LIKE '%Deceased%'
				OR	cm.Comment LIKE '%Died%'
				OR	cm.Comment LIKE '%Removed_Scheme%'
				OR	cm.Comment LIKE '%Pan Deactivated%')
		AND EXISTS (	SELECT 1
						FROM #DeactivatedCustomers dc
						WHERE cm.ObjectID = dc.FanID
						AND dc.ActivationDate <= cm.[Date])
		AND NOT EXISTS (SELECT 1
						FROM #Comm_Deact cmd
						WHERE cm.FanID = cmd.FanID)
		AND NOT EXISTS (SELECT 1
						FROM #DeactTable dt
						WHERE cm.FanID = dt.FanID)
		GROUP BY cm.ObjectID

	---------------------------------------------------------------------------------------
	-------------------------Create a table of Deac dates----------------------------------
	---------------------------------------------------------------------------------------

		/*Where no other date could be found we put in the Activation Date*/
	
		IF OBJECT_ID('tempdb..#Deactivations') IS NOT NULL DROP TABLE #Deactivations;
		WITH
		Deactivations AS (	SELECT	FanID
								,	Deact_Date
							FROM #Comm_Deact
							UNION ALL
							SELECT	FanID
								,	Deact_Date 
							FROM #DeactTable
							UNION ALL
							SELECT	FanID
								,	Deact_Date 
							FROM #Comm_Deact2)
		SELECT	dc.FanID
			,	CASE
					WHEN d.Deact_Date IS NULL THEN dc.ActivationDate
					ELSE d.Deact_Date
				END AS DDate
		INTO #Deactivations
		FROM #DeactivatedCustomers dc
		LEFT JOIN Deactivations d
			ON dc.FanID = d.FanID
	---------------------------------------------------------------------------------------
	--------------------------------Update Customer Table----------------------------------
	---------------------------------------------------------------------------------------

		UPDATE c
		SET DeactivationDate = DDate
		FROM [APW].[DirectLoad_Staging_Customer] c
		INNER JOIN #Deactivations D
			ON c.FanID = D.FanID
		WHERE c.ActivationDate IS NOT NULL
		AND c.CustStatus = 0

	---------------------------------------------------------------------------------------
	--------------------------------Export to RewardBI----------------------------------
	---------------------------------------------------------------------------------------

		SELECT	FanID
			,	PublisherID = CONVERT(TINYINT, 132)
			,	DOB
			,	Gender
			,	ActivationDate
			,	DeactivationDate
			,	SubPublisherID = CONVERT(TINYINT, 0)
		FROM [APW].[DirectLoad_Staging_Customer]

END