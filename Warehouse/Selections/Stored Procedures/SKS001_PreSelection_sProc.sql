

CREATE PROCEDURE Selections.SKS001_PreSelection_sProc
AS
BEGIN

	DECLARE @FIRST_DAY DATE = '2020-07-13'	-- FIND THIS MANUALLY, @FIRST_DAY = 1 MONTH BEFORE THE START DATE OF THE CAMPAIGN THAT IS A WEEK DAY. I.E. CAMPAIGN STARTS THURS 23/07/20, SELECT @FIRST_DAY = TUES 23/06/20
	DECLARE @DAYS_PRIOR_TO_SEG INT = 90		-- 90 DAYS BEFORE TODAY, APPROX 3 MONTHS

	DECLARE @SEGMENTATIONDATE DATE = GETDATE()
	DECLARE @SHOPPERDATE DATE = DATEADD(DAY, -@DAYS_PRIOR_TO_SEG, @SEGMENTATIONDATE)


	-- UNIQUE HOUSEHOLD TABLE WITH BANKACCOUNTIDS
	IF OBJECT_ID('TEMPDB..#UNIQUE_HH_BANKACCOUNTID') IS NOT NULL DROP TABLE #UNIQUE_HH_BANKACCOUNTID
	SELECT	  DISTINCT HouseholdID
			, BankAccountID
	INTO	#UNIQUE_HH_BANKACCOUNTID
	FROM	Warehouse.Relational.MFDD_Households 
	WHERE	EndDate IS NULL
	CREATE CLUSTERED INDEX CIX_HH ON #UNIQUE_HH_BANKACCOUNTID(HouseholdID)


	-- CC TABLE - MARKET
	IF OBJECT_ID('TEMPDB..#CC_DD_COMPETITORS') IS NOT NULL DROP TABLE #CC_DD_COMPETITORS
	SELECT	  ConsumerCombinationID_DD
			, BrandID
	INTO	#CC_DD_COMPETITORS
	FROM	Warehouse.Relational.ConsumerCombination_DD
	WHERE	BrandID IN (395,478,1194,1193)
	CREATE CLUSTERED INDEX CIX_CC_DD_COMPETITORS ON #CC_DD_COMPETITORS(ConsumerCombinationID_DD)

	-- SKY CC TABLE	
	IF OBJECT_ID('TEMPDB..#CC_DD') IS NOT NULL DROP TABLE #CC_DD
	SELECT	ConsumerCombinationID_DD
	INTO	#CC_DD
	FROM	Warehouse.Relational.ConsumerCombination_DD
	WHERE	BrandID = 395
	CREATE CLUSTERED INDEX CIX_CC_DD ON #CC_DD(ConsumerCombinationID_DD)

	-- FIND CURRENT SKY AND MARKET HOUSHOLDS
	IF OBJECT_ID('TEMPDB..#SKY_HOUSEHOLDS_L3M') IS NOT NULL DROP TABLE #SKY_HOUSEHOLDS_L3M
	SELECT	  A.HouseholdID
			, A.BankAccountID
			, SKY_FLAG
			, COMP_FLAG
	INTO	#SKY_HOUSEHOLDS_L3M
	FROM	(
				SELECT	  HouseholdID
						, BankAccountID
				FROM	#UNIQUE_HH_BANKACCOUNTID

			) A
	LEFT JOIN 
			(
				SELECT	  BankAccountID
						, MAX(CASE WHEN BrandID = 395 THEN 1 ELSE 0 END) AS SKY_FLAG
						, MAX(CASE WHEN BRANDID <> 395 THEN 1 ELSE 0 END) AS COMP_FLAG
				FROM	Warehouse.Relational.ConsumerTransaction_DD CT WITH(NOLOCK)
				JOIN	#CC_DD_COMPETITORS CC
					ON  CT.ConsumerCombinationID_DD = CC.ConsumerCombinationID_DD
				WHERE	TranDate >= @SHOPPERDATE -- 90 DAYS BEFORE TODAY, APPROX 3 MONTHS
					AND TranDate <= @SEGMENTATIONDATE -- 
				GROUP BY  BankAccountID

			) B
		ON A.BankAccountID = B.BankAccountID


	-- PER HOUSEHOLD WHO IS A SKY AND COMP SHOPPER
	IF OBJECT_ID('TEMPDB..#SKY_ACCOUNTS_PER_HOUSEHOLD') IS NOT NULL DROP TABLE #SKY_ACCOUNTS_PER_HOUSEHOLD
	SELECT	  HouseholdID
			, MAX(SKY_FLAG) AS SKY_FLAG
			, MAX(COMP_FLAG) AS COMP_FLAG
	INTO	#SKY_ACCOUNTS_PER_HOUSEHOLD
	FROM	#SKY_HOUSEHOLDS_L3M
	GROUP BY  HouseholdID

	-- HOUEHOLDS WITH SKY AND COMP SPEND
	IF OBJECT_ID('TEMPDB..#SKY_AND_COMP_HOUSEHOLDS') IS NOT NULL DROP TABLE #SKY_AND_COMP_HOUSEHOLDS
	SELECT	HouseholdID
	INTO	#SKY_AND_COMP_HOUSEHOLDS
	FROM	#SKY_ACCOUNTS_PER_HOUSEHOLD
	WHERE	SKY_FLAG = 1 AND COMP_FLAG = 1
	CREATE NONCLUSTERED INDEX NCIX_SKY_COMP ON #SKY_AND_COMP_HOUSEHOLDS(HouseholdID)


	-- HOW MANY TRANSACTION WITH SKY IN THE LAST X DAYS

	IF OBJECT_ID('TEMPDB..#SKY_TRANSACTIONS_LAST_90D_SUBSET') IS NOT NULL DROP TABLE #SKY_TRANSACTIONS_LAST_90D_SUBSET
	SELECT	  A.HouseholdID
			, B.BankAccountID 
			, TRANSACTIONS
	INTO	#SKY_TRANSACTIONS_LAST_90D_SUBSET
	FROM	(
				SELECT	  UH.HouseholdID
						, BankAccountID
				FROM	#UNIQUE_HH_BANKACCOUNTID UH
				JOIN	#SKY_AND_COMP_HOUSEHOLDS H
					ON  UH.HouseholdID = H.HouseholdID
			) A
	LEFT JOIN 
			(
				SELECT	  BankAccountID
						, COUNT(1) AS TRANSACTIONS
				FROM	Warehouse.Relational.ConsumerTransaction_DD CT WITH(NOLOCK)
				JOIN	#CC_DD CC
					ON  CT.ConsumerCombinationID_DD = CC.ConsumerCombinationID_DD
				WHERE	TranDate >= @SHOPPERDATE -- 90 DAYS BEFORE TODAY, APPROX 3 MONTHS
					AND TranDate <= @SEGMENTATIONDATE
				GROUP BY  BankAccountID
			) B
		ON A.BankAccountID = B.BankAccountID

	-- THE TARGETING WILL ONLY SELECT THOSE THAT HAVE MADE 3 TRANSACTIONS WITH SKY IN THE LAST 90 DAYS

	/* RF

	Should this potentially consider adding together transactions across bank accounts within each household?
	
	IF OBJECT_ID('TEMPDB..#THREE_TRANSACTIONS') IS NOT NULL DROP TABLE #THREE_TRANSACTIONS
	SELECT	HouseholdID
	INTO	#THREE_TRANSACTIONS
	FROM	(	SELECT	DISTINCT
						HouseholdID
					,	BankAccountID
					,	COALESCE(TRANSACTIONS, 0) AS TRANSACTIONS
				FROM	#SKY_TRANSACTIONS_LAST_90D_SUBSET) st
	GROUP BY HouseholdID
	HAVING	SUM(TRANSACTIONS) = 3
	CREATE CLUSTERED INDEX CIX_HouseholdID ON #THREE_TRANSACTIONS(HouseholdID)

	*/

	IF OBJECT_ID('TEMPDB..#THREE_TRANSACTIONS') IS NOT NULL DROP TABLE #THREE_TRANSACTIONS
	SELECT	DISTINCT
			HouseholdID
	INTO	#THREE_TRANSACTIONS
	FROM	#SKY_TRANSACTIONS_LAST_90D_SUBSET
	WHERE	TRANSACTIONS = 3
	CREATE CLUSTERED INDEX CIX_HouseholdID ON #THREE_TRANSACTIONS(HouseholdID)

	-- FIND THE AMOUNTS AND DATES OF THESE TRANSACTIONS
	IF OBJECT_ID('TEMPDB..#SKY_TRANSACTIONS_LAST_90D_SUBSET_WITH_TRANDETAILS') IS NOT NULL DROP TABLE #SKY_TRANSACTIONS_LAST_90D_SUBSET_WITH_TRANDETAILS
	SELECT	  A.HouseholdID
			, B.BankAccountID 
			, TranDate
			, Amount
			, TRAN_NUMBER
	INTO	#SKY_TRANSACTIONS_LAST_90D_SUBSET_WITH_TRANDETAILS
	FROM	(
				SELECT	  HouseholdID
						, BankAccountID
				FROM	#UNIQUE_HH_BANKACCOUNTID
				WHERE   HouseholdID IN (SELECT HouseholdID FROM	#THREE_TRANSACTIONS)
			) A
	LEFT JOIN 
			(
				SELECT	  BankAccountID
						, Amount
						, TranDate
						, ROW_NUMBER() OVER (PARTITION BY BANKACCOUNTID ORDER BY TRANDATE ASC) AS TRAN_NUMBER
				FROM	Warehouse.Relational.ConsumerTransaction_DD CT WITH(NOLOCK)
				JOIN	#CC_DD CC
					ON  CT.ConsumerCombinationID_DD = CC.ConsumerCombinationID_DD
				WHERE	TranDate >= @SHOPPERDATE -- 90 DAYS BEFORE TODAY, APPROX 3 MONTHS
					AND TranDate <= @SEGMENTATIONDATE
			) B
		ON A.BankAccountID = B.BankAccountID

	-- CLEAN UP THE OUTPUT OF THE TABLE ABOVE

	/*
	RF
	Again could TRAN_NUMBER be on HouseholdID rather than BankAccountID?
	*/

	IF OBJECT_ID('TEMPDB..#PRE_SELECTION') IS NOT NULL DROP TABLE #PRE_SELECTION
	SELECT	  HouseholdID
			, BankAccountID
			, TranDate
			, Amount
			, TRAN_NUMBER
			, ROW_NUMBER() OVER (PARTITION BY HouseholdID ORDER BY TRANDATE ASC) AS TRAN_NUMBER_HOUSEHOLD
	INTO	#PRE_SELECTION
	FROM	#SKY_TRANSACTIONS_LAST_90D_SUBSET_WITH_TRANDETAILS
	WHERE	BankAccountID IS NOT NULL

	-- USE LAG FUNCTIONS TO BRING ALL TRANSACTIONS TO THE SAME LINE
	IF OBJECT_ID('TEMPDB..#PRE_SELECTION_2') IS NOT NULL DROP TABLE #PRE_SELECTION_2
	SELECT	  HouseholdID
			, BankAccountID
			, TranDate AS TRANDATE_3
			, LAG(TranDate,1) OVER (PARTITION BY BANKACCOUNTID ORDER BY TRANDATE) AS TRANDATE_2
			, LAG(TranDate,2) OVER (PARTITION BY BANKACCOUNTID ORDER BY TRANDATE) AS TRANDATE_1
			, Amount AS Amount_3
			, LAG(Amount,1) OVER (PARTITION BY BANKACCOUNTID ORDER BY TRANDATE) AS Amount_2
			, LAG(Amount,2) OVER (PARTITION BY BANKACCOUNTID ORDER BY TRANDATE) AS Amount_1
			, TRAN_NUMBER					
	INTO	#PRE_SELECTION_2
	FROM	#PRE_SELECTION

	
	-- RESTRICT TO JUST ONE ROW PER HOUSEHOLD
	IF OBJECT_ID('TEMPDB..#PRE_SELECTION_3') IS NOT NULL DROP TABLE #PRE_SELECTION_3
	SELECT	  HouseholdID
			, BankAccountID
			, TRANDATE_1
			, TRANDATE_2
			, TRANDATE_3
			, Amount_1
			, Amount_2
			, Amount_3
	INTO	#PRE_SELECTION_3
	FROM	#PRE_SELECTION_2
	WHERE   TRAN_NUMBER = 3

	-- REMOVE THOSE WHO HAVEN'T MADE 3 EQUAL TRANSACTIONS AND THOSE WHO DON'T TRANSACT 1 MONTH APART
	IF OBJECT_ID('TEMPDB..#LAST_3_SKY_TRANS') IS NOT NULL DROP TABLE #LAST_3_SKY_TRANS
	SELECT	  HouseholdID
			, BankAccountID
			, TRANDATE_1
			, TRANDATE_2
			, TRANDATE_3
			, Amount_1
			, Amount_2
			, Amount_3
	INTO	#LAST_3_SKY_TRANS
	FROM	#PRE_SELECTION_3
	WHERE   ((DATEDIFF(DAY,TRANDATE_2,TRANDATE_3) BETWEEN 27 AND 33)
		OR	(DATEDIFF(DAY,TRANDATE_1,TRANDATE_2) BETWEEN 27 AND 33))

	
	-- REMOVE HOUSEHOLDS WITH MORE THAN 1 BANK ACCOUNT THAT HAS A SKY ACCOUNT

	IF OBJECT_ID('TEMPDB..#HOUSEHOLDS_TO_EXCLUDE') IS NOT NULL DROP TABLE #HOUSEHOLDS_TO_EXCLUDE
	SELECT	HouseholdID
	INTO	#HOUSEHOLDS_TO_EXCLUDE
	FROM	#LAST_3_SKY_TRANS
	GROUP BY HouseholdID
	HAVING COUNT(1) > 1


	IF OBJECT_ID('TEMPDB..#FINAL_PRESELECTION') IS NOT NULL DROP TABLE #FINAL_PRESELECTION
	SELECT	HouseholdID
			, BankAccountID
			, TRANDATE_1
			, TRANDATE_2
			, TRANDATE_3
			, Amount_1
			, Amount_2
			, Amount_3
	INTO	#FINAL_PRESELECTION
	FROM	#LAST_3_SKY_TRANS
	WHERE	HouseholdID NOT IN (SELECT HouseholdID FROM #HOUSEHOLDS_TO_EXCLUDE)
		OR  TRANDATE_3 <> @FIRST_DAY -- SHOULD EXCLUDE EVERYONE WHO WILL GO ON TO TRANSACT ON THE FIRST DAY OF THE OFFER.

	

	-- FINAL FEW STEPS TO GET A LIST OF SOURCEUIDS FOR RORY
	IF OBJECT_ID('TEMPDB..#UNIQUE_HH_SOURCEUID') IS NOT NULL DROP TABLE #UNIQUE_HH_SOURCEUID
	SELECT	  DISTINCT HouseholdID
			, FanID
	INTO	#UNIQUE_HH_SOURCEUID
	FROM	Warehouse.Relational.MFDD_Households
	WHERE	EndDate IS NULL

	
	IF OBJECT_ID('TEMPDB..#SOURCEUIDS_FOR_SKY_SHOPPER_OFFER') IS NOT NULL DROP TABLE #SOURCEUIDS_FOR_SKY_SHOPPER_OFFER
	SELECT	FanID
	INTO	#SOURCEUIDS_FOR_SKY_SHOPPER_OFFER
	FROM	#UNIQUE_HH_SOURCEUID
	WHERE	HouseholdID IN (SELECT HouseholdID FROM #FINAL_PRESELECTION)

	IF OBJECT_ID('SANDBOX.CONAL.SKY_SHOPPER_300720') IS NOT NULL DROP TABLE SANDBOX.CONAL.SKY_SHOPPER_300720
	SELECT	FanID
	INTO	SANDBOX.CONAL.SKY_SHOPPER_300720
	FROM	#UNIQUE_HH_SOURCEUID
	WHERE	HouseholdID IN (SELECT HouseholdID FROM #FINAL_PRESELECTION)

	IF OBJECT_ID('Warehouse.Selections.SKS001_PreSelection') IS NOT NULL DROP TABLE Warehouse.Selections.SKS001_PreSelection
	SELECT	FanID
	INTO Warehouse.Selections.SKS001_PreSelection
	FROM SANDBOX.CONAL.SKY_SHOPPER_300720	
	

END