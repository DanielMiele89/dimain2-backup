CREATE PROCEDURE [dbo].[MigrationCheck_WHB_Customer] AS 

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE @RowCount BIGINT

IF 1 = 1 BEGIN
	IF OBJECT_ID('tempdb..#CustomerDIMAIN2') IS NOT NULL DROP TABLE #CustomerDIMAIN2;
	SELECT FanID, [hash] = CHECKSUM(*) 
	INTO #CustomerDIMAIN2
	FROM Warehouse.relational.Customer
	SET @RowCount = @@ROWCOUNT
	CREATE UNIQUE CLUSTERED INDEX ucx_FanID ON #CustomerDIMAIN2 (FanID)
	-- (31733812 rows affected) / 00:05:03

	IF OBJECT_ID('tempdb..#CustomerDIDEVTEST') IS NOT NULL DROP TABLE #CustomerDIDEVTEST;
	SELECT FanID, [hash] 
	INTO #CustomerDIDEVTEST 
	FROM OPENQUERY (DIDEVTEST, 'SELECT FanID, [hash] = CHECKSUM(*) FROM Warehouse.relational.Customer')
	CREATE UNIQUE CLUSTERED INDEX ucx_FanID ON #CustomerDIDEVTEST (FanID)
	-- (31733812 rows affected) / 00:06:05

	IF OBJECT_ID('tempdb..#CustomerDIMAIN') IS NOT NULL DROP TABLE #CustomerDIMAIN;
	SELECT FanID, [hash] 
	INTO #CustomerDIMAIN 
	FROM OPENQUERY (DIMAIN, 'SELECT FanID, [hash] = CHECKSUM(*) FROM Warehouse.relational.Customer')
	CREATE UNIQUE CLUSTERED INDEX ucx_FanID ON #CustomerDIMAIN (FanID)
	-- (31733812 rows affected) / 00:17:07
END

SELECT TableName = 'Relational.Customer', [Date] = GETDATE(), [RowCount] = @RowCount

;WITH NewRows AS (
	SELECT [Description] = 'DIDEVTEST-DIMAIN2', [RowCount] = COUNT(*) FROM (SELECT FanID FROM #CustomerDIDEVTEST EXCEPT SELECT FanID FROM #CustomerDIMAIN2) d -- 0
	UNION ALL SELECT 'DIMAIN2-DIDEVTEST', COUNT(*) FROM (SELECT FanID FROM #CustomerDIMAIN2 EXCEPT SELECT FanID FROM #CustomerDIDEVTEST) d -- 0
	UNION ALL
	SELECT 'DIMAIN-DIDEVTEST', COUNT(*) FROM (SELECT FanID FROM #CustomerDIMAIN EXCEPT SELECT FanID FROM #CustomerDIDEVTEST) d -- 0
	UNION ALL SELECT 'DIDEVTEST-DIMAIN', COUNT(*) FROM (SELECT FanID FROM #CustomerDIDEVTEST EXCEPT SELECT FanID FROM #CustomerDIMAIN) d -- 0
	UNION ALL
	SELECT 'DIMAIN2-DIMAIN', COUNT(*) FROM (SELECT FanID FROM #CustomerDIMAIN2 EXCEPT SELECT FanID FROM #CustomerDIMAIN) d -- 0
	UNION ALL SELECT 'DIMAIN-DIMAIN2', COUNT(*) FROM (SELECT FanID FROM #CustomerDIMAIN EXCEPT SELECT FanID FROM #CustomerDIMAIN2) d -- 0
), ChangedRows AS (
	SELECT [Description] = 'DIDEVTEST-DIMAIN2', [RowCount] = COUNT(*) FROM (SELECT * FROM #CustomerDIDEVTEST EXCEPT SELECT * FROM #CustomerDIMAIN2) d -- 17
	UNION ALL SELECT 'DIMAIN2-DIDEVTEST', COUNT(*) FROM (SELECT * FROM #CustomerDIMAIN2 EXCEPT SELECT * FROM #CustomerDIDEVTEST) d -- 17
	UNION ALL
	SELECT 'DIMAIN-DIDEVTEST', COUNT(*) FROM (SELECT * FROM #CustomerDIMAIN EXCEPT SELECT * FROM #CustomerDIDEVTEST) d -- 311
	UNION ALL SELECT 'DIDEVTEST-DIMAIN', COUNT(*) FROM (SELECT * FROM #CustomerDIDEVTEST EXCEPT SELECT * FROM #CustomerDIMAIN) d -- 311
	UNION ALL
	SELECT 'DIMAIN2-DIMAIN', COUNT(*) FROM (SELECT * FROM #CustomerDIMAIN2 EXCEPT SELECT * FROM #CustomerDIMAIN) d -- 296
	UNION ALL SELECT 'DIMAIN-DIMAIN2', COUNT(*) FROM (SELECT * FROM #CustomerDIMAIN EXCEPT SELECT * FROM #CustomerDIMAIN2) d -- 296
)
SELECT n.[Description], n.[RowCount] AS [New rows], c.[RowCount]-n.[RowCount] AS [Changed Rows]
FROM NewRows n
INNER JOIN ChangedRows c 
	ON c.[Description] = n.[Description]
-- 6 / 00:04:20


RETURN 0

